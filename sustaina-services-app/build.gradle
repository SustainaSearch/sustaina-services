apply plugin: 'play'
apply plugin: 'scala'

dependencies {
    compile project(':sustaina-index:core')
    compile project(':sustaina-services-api-model')

    compile "com.typesafe.play:play-cache_$scalaPlatform:$playVersion"
    compile "com.typesafe.play:play-guice_$scalaPlatform:$playVersion"

    compile "io.swagger:swagger-play2_$scalaPlatform:$swaggerVersion"

    // For generating Flow types
    compile "com.davegurnell:bridges_2.12:0.20.2"

    compile("com.pauldijou:jwt-play_2.12:$pauldijouJwtVersion") {
        transitive = false
    }
    compile "com.pauldijou:jwt-core_2.12:$pauldijouJwtVersion"
    compile "com.auth0:jwks-rsa:$auth0Version"

//    testCompile  "org.scalatestplus.play:scalatestplus-play:3.0.0"
    testCompile "junit:junit:$junitVersion"
    testCompile "org.scalatest:scalatest_$scalaPlatform:$scalaTestVersion"
}

model {
    components {
        play {
            platform play: playVersion, scala: scalaPlatform, java: '1.8'
            injectedRoutesGenerator = true

            sources {
                twirlTemplates {
                    defaultImports = TwirlImports.SCALA
                }
            }
        }
    }

    distributions {
        playBinary {
            baseName = "sustaina-services"
        }
    }
}

idea {
    module {
        sourceDirs += file("app")
        testSourceDirs += file("test")
        scopes.COMPILE = [plus: [configurations.play], minus: []]
        scopes.RUNTIME = [plus: [configurations.playRun], minus:[configurations.play]]
        scopes.TEST = [plus: [configurations.playTest], minus: [configurations.playRun]]
    }
}

configurations {
    play {
        extendsFrom compile
    }
}

// TODO: make it possible to define repositories only once in root project build.gradle
repositories {
    jcenter()
    maven {
        name "lightbend-maven-releases"
        url "https://repo.lightbend.com/lightbend/maven-release"
    }
    ivy {
        name "lightbend-ivy-release"
        url "https://repo.lightbend.com/lightbend/ivy-releases"
        layout "ivy"
    }
}